managed implementation in class zbp_i_jarap_certifproduct unique;
strict ( 1 );
with draft;

define behavior for ZI_JARAP_CERTIFPRODUCT alias Certificate
persistent table zjarap_certif
draft table zbjarap_certif
lock master total etag LocalLastChangedAt
authorization master ( instance )
//etag master <field_name>
{
  create;
  update;
  delete;

  association _Stats { create; with draft; }

  draft action Edit;
  draft action Activate;
  draft action Discard;
  draft action Resume;
  draft determine action Prepare;

  determination setInitialValues on modify { create; }

  validation checkMaterial on save { field Matnr; create; update; }

  action ( features : instance ) NewVersion result [1] $self;
  action ( features : instance ) ActiveVersion result [1] $self;
  action ( features : instance ) InactiveVersion result [1] $self;

  field ( numbering : managed, readonly ) CertUuid;
  field ( readonly ) Description, Version;
  field ( mandatory ) Matnr;

  mapping for zjarap_certif
  {
    CertUuid = cert_uuid;
    Matnr = matnr;
    Version = version;
    CertStatus = cert_status;
    CertCe = cert_ce;
    CertGs = cert_gs;
    CertFcc = cert_fcc;
    CertIso = cert_iso;
    CertTuev = cert_tuev;
    LocalLastChangedAt = local_last_changed_at;
  }
}

define behavior for ZI_JARAP_CERTIFSTPRODUCT alias CertificateST
persistent table zjarap_certif_st
draft table zbjarap_certifst
lock dependent by _Certif
authorization dependent by _Certif
//etag master <field_name>
{
  //update;
  delete;

  field ( readonly ) CertUuid;
  field ( numbering : managed, readonly ) StateUuid;

  association _Certif;

  mapping for zjarap_certif_st
  {
    StateUuid = state_uuid;
    CertUuid = cert_uuid;
    Matnr = matnr;
    Version = version;
    Status = status;
    StatusOld = status_old;
    LastChangedBy = last_changed_by;
    LastChangedAt = last_changed_at;
    LocalLastChangedAt = local_last_changed_at;
  }

}
